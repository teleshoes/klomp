#!/usr/bin/perl
use strict;
use warnings;

my $limit = 50;

my $okSuffixes = join "|", qw(mp3 flac ogg m4a m4b wma wav);

sub main(@){
  my $history = `klomp-lib -p history`;
  chomp $history;
  $history =~ s/^\s+//;
  $history =~ s/\s+$//;
  die "no history dir\n" if $history eq "" or not -d $history;

  chdir $history;
  $ENV{PWD} = $history;
  my @commits = `git log --format=format:%H`;

  my %dates;
  my %flacCount;
  my %oggCount;
  my %otherCount;
  my @dirOrder;
  for my $c(@commits){
    my $date = `git log -n 1 --format=format:%ar $c`;
    my @paths = `git log -n 1 --format=format: --name-only $c`;
    for my $p(@paths){
      next if $p !~ /\.([a-zA-Z0-9]+)"?/;
      my $ext = $1;
      next if $ext !~ /$okSuffixes/i;

      my $dir = $p;
      next if $dir !~ /\//;
      $dir =~ s/\/[^\/]*$//;
      $dir =~ s/^"//;
      $dir =~ s/^libs\///;

      $flacCount{$dir} = 0 if not defined $flacCount{$dir};
      $oggCount{$dir} = 0 if not defined $oggCount{$dir};
      $otherCount{$dir} = 0 if not defined $otherCount{$dir};

      if($ext =~ /flac/i){
        $flacCount{$dir}++;
      }elsif($ext =~ /ogg/i){
        $oggCount{$dir}++;
      }else{
        $otherCount{$dir}++;
      }

      if(not defined $dates{$dir}){
        push @dirOrder, $dir;
        $dates{$dir} = $date;
      }
    }
    last if @dirOrder > $limit;
  }
  @dirOrder = @dirOrder[0..$limit-1] if @dirOrder > $limit;
  for my $dir(@dirOrder){
    my @cnts;
    push @cnts, "$flacCount{$dir} flac" if $flacCount{$dir} > 0;
    push @cnts, "$oggCount{$dir} ogg" if $oggCount{$dir} > 0;
    push @cnts, "$otherCount{$dir} other" if $otherCount{$dir} > 0;
    print "$dir => $dates{$dir} {" . (join ", ", @cnts) . "}\n";
  }
}

&main(@ARGV);
