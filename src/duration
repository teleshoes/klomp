#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long qw(GetOptionsFromArray :config bundling);
use lib "/opt/klomp/lib";
use Klomp::Duration qw(getDuration cmdExists formatTimeS formatTimeHMS);

my $usage = "Usage:
  $0 [OPTS] FILENAME FILENAME ..
    ffmpeg {or avconv} frontend to print the durations of sound files
    Prints one per line:  'H:MM:SS FILENAME' or 'M:SS FILENAME'
  $0 -h|--help
    Print this message

  -s, --seconds
     Print durations in 'S.FFFFFF' instead of 'M:SS' or 'H:MM:SS'
  -t, --total
     Print total of all files instead of individually.
  -n, --nofilename
     Do not print the filename after the duration.
  --trunc
     Truncate fractional seconds instead of rounding half-up
     Print seconds as an integer with --seconds
     e.g.: 159.61 [2:40]  => 159 [2:39]
";

sub main(@){
  my ($help, $seconds, $total, $nofilename, $trunc);
  my $ok = GetOptionsFromArray(\@_,
    "h|help"       => \$help,
    "s|seconds"    => \$seconds,
    "t|total"      => \$total,
    "n|nofilename" => \$nofilename,
    "trunc"        => \$trunc,
  ) or die $usage;
  my @files = @_;

  die $usage if not $ok or @files == 0 or $help;
  die "FATAL: no ffmpeg/avconv cmd found\n" if not cmdExists;

  my $lenTotal = 0;
  for my $file(@_){
    if(not -f $file){
      print STDERR "$file not found\n";
      next;
    }
    my $len = getDuration $file;
    if(not defined $len){
      print STDERR "Unknown duration for $file\n";
      next;
    }
    $lenTotal += $len;

    $len = int($len) if $trunc;

    my $fmt;
    if($seconds){
      $fmt = $len;
    }else{
      $fmt = formatTimeHMS($len);
    }
    $fmt .= " $file" unless $nofilename;
    $fmt .= "\n";
    print $fmt unless $total;
  }
  if($total){
    my $fmtTotal;
    if($seconds){
      $fmtTotal = $trunc ? int($lenTotal) : formatTimeS($lenTotal);
    }else{
      $fmtTotal = $trunc ? formatTimeHMS(int($lenTotal)) : formatTimeHMS($lenTotal);
    }
    print "$fmtTotal\n" if $total;
  }
}

&main(@ARGV);
