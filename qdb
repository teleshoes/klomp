#!/usr/bin/perl
use strict;
use warnings;

#sudo apt-get install libaudio-wma-perl lltag
#sudo cpan Lingua::JA::Romanize::Japanese

sub shell(@);
sub shellQuiet(@);
sub runSql($;@);
sub cols($);
sub guess($$$$$$$$);
sub transliterateJapanese($);
sub addSongs($$$);
sub wrapGuessCol($);
sub create();
sub update($);
sub missing($);
sub add($$);
sub search($$\@);
sub info($);
sub readTags($);

my $useAudioWma;
BEGIN{
  $useAudioWma = eval{require Audio::WMA;};
};

my @songSuffixes = ('mp3', 'flac', 'ogg', 'm4a', 'wma', 'wav');
                    #aiff

my @allowedCols = qw(libpath relpath filesize modified md5sum
                     title artist album number date genre);

my $usage = "Usage: 
 $0 [db-path] -c|--create
   Make an empty sqlite db

 $0 [db-path] -u|--update library-dir
   Add or update the info in the db for all the music files in library-dir

 $0 [db-path] -m|--missing library-dir
   Check for missing files, and prompt for removal.

 $0 [db-path] -a|--add library-dir song-rel-path
   Add or update the info in the db for the file at library-dir/song-rel-path

 $0 [db-path] -s|--search query [OPTS]
   Query for relative paths to songs, or more information
   optional OPTS:
   -l|--limit LIMIT        optional limit on the number of rows returned
   --columns  COL COL ...  optional columns to return as csv.
     by default, just relpath is returned. possible values include:
     @allowedCols

 $0 [db-path] -i|--info [song-rel-path or song-abs-path]
   Query for the info for a song, given the path of the song
";

die $usage if @ARGV < 2 or $ARGV[0] eq '-h' or $ARGV[0] eq '--help';

my $db = shift;
my $cmd = shift;

if($cmd eq '-c' or $cmd eq '--create' and @ARGV == 0){
  create();
}elsif($cmd eq '-u' or $cmd eq '--update' and @ARGV == 1){
  my $libDir = shift();
  update($libDir);
}elsif($cmd eq '-m' or $cmd eq '--missing' and @ARGV == 1){
  my $libDir = shift();
  missing($libDir);
}elsif($cmd eq '-a' or $cmd eq '--add' and @ARGV == 2){
  my $libDir = shift();
  my $songRelPath = shift();
  add($libDir, $songRelPath);
}elsif($cmd eq '-s' or $cmd eq '--search' and @ARGV >= 2){
  my $query = shift;

  my $limit = 0;
  my @cols;
  my $mode = 'playlist';
  while(@ARGV){
    my $arg = shift();
    if(($arg eq '-l' or $arg eq '--limit') && @ARGV>0){
      $limit = shift();
      if(not defined $limit or $limit !~ /^\d+$/){
        die $usage;
      }
    }elsif(($arg eq '--columns') && @ARGV>0){
      my %allowed;
      for my $allow(@allowedCols){
        $allowed{$allow} = 1;
      }
      while(@ARGV){
        my $arg = shift;
        if(not defined $allowed{$arg}){
          die $usage;
        }
        push @cols, $arg;
      }
    }else{
      die $usage;
    }
  }

  search($query, $limit, @cols);
}elsif($cmd eq '-i' or $cmd eq '--info' and @ARGV == 1){
  my $songRelPath = shift();
  info($songRelPath);
}else{
  die $usage;
}


sub create(){
  if(-e $db){
    die "$db exists already\n";
  }
  runSql(
    "create table Songs (".
      "libpath varchar,".
      "relpath varchar,".
      "filesize int,".
      "modified int,".
      "md5sum varchar,".
      "title varchar,".
      "artist varchar,".
      "album varchar,".
      "number varchar,".
      "date varchar,".
      "genre varchar,".
      "title_guess varchar,".
      "artist_guess varchar,".
      "album_guess varchar,".
      "number_guess varchar,".
      "date_guess varchar,".
      "genre_guess varchar,".
      "PRIMARY KEY (libpath, relpath)".
    ");");
}

sub update($){
  my $lib = shift;
  chdir $lib;

  my $suffixPattern;
  for(my $i=0; $i<@songSuffixes; $i++){
    $suffixPattern .= ".*\\." . $songSuffixes[$i];
    if($i < $#songSuffixes){
      $suffixPattern .= "\\|";
    }
  }
  my @songRelPaths = shell 'find', '.', '-iregex', $suffixPattern;
  for (my $i=0; $i<@songRelPaths; $i++){
    my $songRelPath = $songRelPaths[$i];
    chomp $songRelPath;
    $songRelPath =~ s/^.\///;
    $songRelPaths[$i] = $songRelPath;
  }
  addSongs($db, $lib, \@songRelPaths);
}

sub missing($){
  my $lib = shift;
  chdir $lib;
  checkMissing($db, $lib);
}

sub add($$){
  my $lib = shift;
  my $songRelPath = shift;
  
  my @songRelPaths;
  push @songRelPaths, $songRelPath;
  addSongs($db, $lib, \@songRelPaths);
}

sub unfoldQuery($){
  my $q = shift;
}

sub parseQuotes($){
  my $query = shift;

  my $newQuery = '';
  my $endOfLastQuote = 0;

  my %quoteAtoms;
  my $atomId = 0;

  my $startQuote=0;
  my $endQuote;
 
  my $offset=0;

  my $len = length $query;
  while($offset >= 0 and $offset<$len){
    last if ($offset = index $query, '"', $offset) < 0;
    $startQuote = $offset;
    $offset++;
    while($offset >= 0 and $offset<$len){
      last if ($offset = index $query, '"', $offset) < 0;
      $endQuote = $offset;
      $offset++;
      my $nextChar = substr $query, $endQuote+1, 1;
      if($nextChar eq '"'){
        $offset++;
      }else{
        my $quoteAtom = substr $query, $startQuote+1, $endQuote-$startQuote-1;
        $quoteAtom =~ s/""/"/g;
        $quoteAtom =~ s/@/~at~/g;
        $quoteAtom =~ s/!/~bang~/g;
        $newQuery .= substr $query, $endOfLastQuote, $startQuote-$endOfLastQuote;
        $endOfLastQuote = $endQuote+1;
        $newQuery .= "~$atomId~";
        $quoteAtoms{$atomId++} = $quoteAtom;
        last;
      }
    }
  }
  $newQuery .= substr $query, $endOfLastQuote, $len-$endOfLastQuote;

  return ($newQuery, %quoteAtoms);
}

sub search($$\@){
  my $query = shift;
  my $limit = shift;
  my @cols = @{shift()};

  if(not @cols){
    @cols = ('relpath');
  }

  my @guessCols;
  my @namedCols;
  for my $col(@cols){
    my $guessCol;
    if($col eq 'artist' or $col eq 'album' or $col eq 'title' or $col eq 'number'){
      $guessCol = wrapGuessCol($col);
    }else{
      $guessCol = $col;
    }
    push @guessCols, $guessCol;
    push @namedCols, "$guessCol $col";
  }

  my $orderCols = join ",\n      ", @guessCols;
  my $orderBy = "ORDER BY\n      $orderCols";

  my $lim = $limit > 0 ? "LIMIT $limit" : '';

  $query =~ s/~/~~/g;
  
  my %quoteAtoms;
  ($query, %quoteAtoms) = parseQuotes $query;
  
  $query =~ s/\s*\&\&\s*/~and~/g;
  $query =~ s/\s*\|\|\s*/~or~/g;
  $query =~ s/\s*€\s*/~or~/g;  #euro symbol is synonym for ||
  $query =~ s/\s+/~and~/g;
  

  for my $atomId(keys %quoteAtoms){
    my $atom = $quoteAtoms{$atomId};
    $query =~ s/~$atomId~/$atom/;
  }

  my $selCols = join "\n       ,", @namedCols;
  my $sql = ''
    . "select $selCols \n"
    . "from Songs \n"
    . "where (\n  ";

  $query .= '~term~';
  my @subqueries;
  while($query =~ /(.*?)(~and~|~or~|~term~)/g){
    push @subqueries, [$1, $2];
  }
  for my $sub(@subqueries){
    my $q = $$sub[0];

    my $neg = '';
    my $negAndOr = 'or';
    if($q =~ /^!/){
      $q =~ s/^!//;
      $neg = 'not';
      $negAndOr = 'and';
    }
    
    my $default = 1;
    my $title = 0;
    my $album = 0;
    my $artist = 0;
    my $genre = 0;
    my $path = 0;
    while($q =~ /^@[tlagp]/){
      $default = 0;
      $q =~ s/^@([tlagp])//;
      $title = 1 if $1 eq 't';
      $album = 1 if $1 eq 'l';
      $artist = 1 if $1 eq 'a';
      $genre = 1 if $1 eq 'g';
      $path = 1 if $1 eq 'p';
    }

    $q =~ s/~bang~/!/g;
    $q =~ s/~at~/@/g;
    
    $q =~ s/~~/~/g;
    $q =~ s/'/''/g;
    $q =~ s/%/\\%/g;
    my $term;
    $term = " and (" if $$sub[1] eq '~and~';
    $term = " or (" if $$sub[1] eq '~or~';
    $term = "" if $$sub[1] eq '~term~';
    my @wheres;
    if($default or $title){
      push @wheres, "title $neg like '\%$q\%'"
                    . " $negAndOr title_guess $neg like '\%$q\%'\n  ";
    }
    if($default or $album){
      push @wheres, "album $neg like '\%$q\%'"
                    . " $negAndOr album_guess $neg like '\%$q\%'\n  ";
    }
    if($default or $artist){
      push @wheres, "artist $neg like '\%$q\%'"
                    . " $negAndOr artist_guess $neg like '\%$q\%'\n  ";
    }
    if($genre){
      push @wheres, "genre $neg like '\%$q\%'\n  ";
    }
    if($path){
      push @wheres, "relpath $neg like '\%$q\%'\n  ";
    }
    $sql .= join(" $negAndOr ", @wheres) . ")$term\n  "
  }
  $sql .= "\n$orderBy\n$lim;";
  
  #print STDERR $sql;
  
  if(@cols > 1){
    print runSql $sql, '-csv';
  }else{
    print runSql $sql;
  }
}

sub info($){
  my $songRelPath = shift;

  $songRelPath =~ s/'/''/g;
  my @rows = runSql(
    "select * ".
    "from Songs ".
    "where relpath = '$songRelPath' or libpath || '/' || relpath = '$songRelPath';",
    '-csv',  '-header');
  if(@rows != 2){
    die "file not found in db: $songRelPath";
  }

  my @headers = cols $rows[0];
  my @data = cols $rows[1];

  if(@headers != @data){
    die "mismatched headers and data\n";
  }
  for(my $i=0; $i<@headers; $i++){
    print $headers[$i] . "=" . $data[$i] . "\n";
  }
}

sub wrapTokens(@){
  my @tokens;
  for my $token(@_){
    my $t = $token;
    $t =~ s/'/'\\''/g;
    push @tokens, "'$t'";
  }
  return @tokens;
}
sub shellQuiet(@){
  my $cmd = join ' ', wrapTokens(@_);
  return `$cmd 2>/dev/null`;
}
sub shell(@){
  my $cmd = join ' ', wrapTokens(@_);
  return `$cmd`;
}

sub runSql($;@){
  my $sql = shift;

  utf8::decode($sql);
  my @tokens;
  push @tokens, 'sqlite3';
  for my $opt(@_){
    push @tokens, $opt;
  }
  push @tokens, $db;
  push @tokens, $sql;

  return shell @tokens;
}

sub cols($){
  my $csvrow = shift;
  chomp $csvrow;
  my @cols;
  while($csvrow =~ /
    (?:
      "()"
      |
      "(.*?(?<!"))"
      |
      ([^,]+)
    )
    (?:,|$)
  /gsxi){
    my $col;
    $col = $1 if defined $1;
    $col = $2 if defined $2;
    $col = $3 if defined $3;
    $col =~ s/""/"/g;
    push @cols, $col;
  }
  return @cols;
}

sub guess($$$$$$$$){
  my ($lib, $songRelPath,
        $title, $artist, $album, $number, $date, $genre) = @_;
  my ($title_guess, $artist_guess, $album_guess,
    $number_guess, $date_guess, $genre_guess);

  $title_guess = transliterateJapanese $title;
  $artist_guess = transliterateJapanese $artist;
  $album_guess = transliterateJapanese $album;
  $number_guess = transliterateJapanese $number;
  $date_guess = transliterateJapanese $date;
  $genre_guess = transliterateJapanese $genre;

  my ($filename, $innerDir, $outerDir);
  if($songRelPath =~ /([^\/]*) \/ ([^\/]*) \/ ([^\/]*)$/x){
    $filename = $3;
    $innerDir = $2;
    $outerDir = $1;
  }elsif($songRelPath =~ /([^\/]*) \/ ([^\/]*)$/x){
    $filename = $2;
    $innerDir = '';
    $outerDir = $1;
  }elsif($songRelPath =~ /([^\/]*)$/x){
    $filename = $1;
    $innerDir = '';
    $outerDir = '';
  }

  if(not defined $title or length $title == 0){
    $title_guess = $filename;
    my $suffixRegex;
    for (my $i=0; $i<@songSuffixes; $i++){
      $suffixRegex .= '.' . $songSuffixes[$i];
      if($i < $#songSuffixes){
        $suffixRegex .= '|';
      }
    }
    $title_guess =~ s/($suffixRegex)$//i;
    $title_guess =~ s/^[0-9 \-_]+//;
    #$title_guess =~ s/_/ /g;
  }
  if(not defined $artist or length $artist == 0){
    $artist_guess = $outerDir;
    #$artist_guess =~ s/_/ /g;
  }
  if(not defined $album or length $album == 0){
    $album_guess = $innerDir;
    #$album_guess =~ s/_/ /g;
  }
  if(not defined $number or length $number == 0){
    if($filename =~ /^\s*(\d+)\s*/){
      $number_guess = $1;
    }
  }
  if(not defined $date or length $date == 0){
    if($songRelPath =~ /(19\d\d|20\d\d)/){
      $date_guess = $1;
    }
  }

  $title_guess = '' if not defined $title_guess;
  $artist_guess = '' if not defined $artist_guess;
  $album_guess = '' if not defined $album_guess;
  $number_guess = '' if not defined $number_guess;
  $date_guess = '' if not defined $date_guess;
  $genre_guess = '' if not defined $genre_guess;

  return ($title_guess, $artist_guess, $album_guess, $number_guess,
    $date_guess, $genre_guess);
}

sub transliterateJapanese($){
  my $arg = shift;
  if(not defined $arg or length $arg == 0){
    return $arg;
  }
  utf8::decode($arg);
  my @parts;
  my $theRest;
  while($arg =~/
      (.*?)
      ((?:\p{Hiragana}|\p{Katakana}|\p{Han})+)
      (?=(.*$))
    /gsxi){
    my $nonjap = $1;
    my $jap = $2;
    $theRest = $3;
    my $romaji = shell 'jap2romaji', $jap;
    push @parts, $nonjap;
    if(not $? and defined $romaji){
      $romaji =~ s/\s+//g;
      push @parts, $romaji;
    }else{
      push @parts, $jap;
    }
  }
  utf8::encode($arg);
  if(@parts > 0){
    push @parts, $theRest;
    my $res = join '', @parts;
    utf8::encode($res);
    return $res;
  }
  return undef;
}

sub checkMissing($$){
  my $db = shift;
  my $lib = shift;
  $lib =~ s/\/$//;
  $lib =~ s/'/''/g;
  print "checking for missing songs, fetching from db\n";
  my @songRelPaths = runSql "select relpath from songs where libpath = '$lib'";
  my $len = @songRelPaths;
  my $piece = 1+$len / 10;
  print "checking $len songs\n";
  my $count = 0;
  my @missing;
  my $deleteSql = '';
  for my $songRelPath(@songRelPaths){
    chomp $songRelPath;
    if(not -e "$lib/$songRelPath"){
      print "$songRelPath\n";
      push @missing, $songRelPath;
      $songRelPath =~ s/'/''/g;
      $deleteSql .= ''
          . "delete from Songs "
          . "where relpath = '$songRelPath' "
          . "  and libpath = '$lib';\n"
          ;
    }
    if($count++ % $piece == 0){
      print "  checked $count / $len songs\n";
    }
  }
  my $missingCount = scalar @missing;
  print "finished, $missingCount missing\n";
  if($missingCount > 0){
    print "Remove above missing files from lib? (y/N) ";
    if(<STDIN> =~ /^y$/i){
      runSql($deleteSql);
    }
  }
}

sub addSongs($$$){
  my $db = shift;
  my $lib = shift;
  $lib =~ s/\/$//;
  my @songRelPaths = @{scalar shift};

  print "loading filesize and modified into memory from db\n";
  my %dbInfo;
  for my $line(runSql('select filesize, modified, relpath from Songs')){
    $line =~ /(\d+)\|(\d+)\|(.*)\n/;
    my @fs_mod = ($1, $2);
    $dbInfo{$3} = \@fs_mod;
  }

  my $count = 0;
  my $limit = 30;
  my $sql = 'BEGIN;';
  for my $songRelPath(@songRelPaths){
    my $sqlPath = $songRelPath;
    $sqlPath =~ s/'/''/g;

    my ($dbSize, $dbMod);
    
    my $info = $dbInfo{$songRelPath};
    my $dbExists = defined $info;
    if($dbExists){
      my @infoArr = @{$info};
      $dbSize = $infoArr[0];
      $dbMod = $infoArr[1];
    }
  
    my $stat = shellQuiet 'stat', '--format', '%s:%Y', "$lib/$songRelPath";
    if($? or $stat !~ /(\d+):(\d+)/){
      print STDERR "file not found: $lib/$songRelPath\n";
      next;
    }
    my $filesize = $1;
    my $modified = $2;

    if($dbExists and $dbSize == $filesize and $dbMod == $modified){
      print "Same size and modified date, skipping $lib/$songRelPath\n";
      next;
    }
    
    my $md5sum = '';
    #my $md5sum = shell 'md5sum', "$lib/$songRelPath";
    #$md5sum = substr $md5sum, 0, 32;

    #if($dbExists and $dbMd5 eq $md5sum){
    #  print "Same md5sum, skipping $songRelPath, but setting new modified\n";
    #  runSql(
    #    "update Songs ".
    #    "set modified=$modified ".
    #    "where path='$sqlPath'");
    #  exit 0;
    #}

    my ($path, $title, $artist, $album, $number, $date, $genre) =
      readTags("$lib/$songRelPath");

    $title =~ s/'/''/g;
    $artist =~ s/'/''/g;
    $album =~ s/'/''/g;
    $number =~ s/'/''/g;
    $date =~ s/'/''/g;
    $genre =~ s/'/''/g;

    my ($title_guess, $artist_guess, $album_guess,
        $number_guess, $date_guess, $genre_guess) = guess(
          $lib, $songRelPath,
          $title, $artist, $album, $number, $date, $genre);

    $title_guess =~ s/'/''/g;
    $artist_guess =~ s/'/''/g;
    $album_guess =~ s/'/''/g;
    $number_guess =~ s/'/''/g;
    $date_guess =~ s/'/''/g;
    $genre_guess =~ s/'/''/g;
   
    print "$sqlPath\n";
    $sql .= "delete from Songs where relpath='$sqlPath';";
    $sql .= 
      "insert into Songs ".
      "(libpath, relpath, filesize, modified, md5sum, ".
        "title, artist, album, number, date, genre, ".
        "title_guess, artist_guess, album_guess, number_guess, ".
        "date_guess, genre_guess) ".
      "values".
      "('$lib', '$sqlPath', $filesize, $modified, '$md5sum', ".
        "'$title', '$artist', '$album', '$number', '$date', '$genre', ".
        "'$title_guess', '$artist_guess', '$album_guess', '$number_guess', ".
        "'$date_guess', '$genre_guess');";
    $count++;
    if($count == $limit){
      print "  running last $limit\n";
      runSql($sql.'END;');
      $sql = 'BEGIN;';
      $count = 0;
    }
  }
  if($count > 0){
    print "  running remaining $count\n";
    runSql($sql.'END;');
  }
}

sub wrapGuessCol($){
  my $col = shift;
  my $colGuess = $col . '_guess';
  return
    "IFNULL(".
      "NULLIF(".
        "$col,".
        "''".
      "),".
      "$colGuess)";
}

sub readTags($){
  my $file = shift;
  my $path = $file;
  $file =~ s/'/'\\''/g;

  my $title='';
  my $artist='';
  my $album='';
  my $number='';
  my $date='';
  my $genre='';
  if($file =~ /\.mp3$/i){
    my $format = ''
                 . 'SUCCESS\n'
                 . 'TITLE=%t\n'
                 . 'ARTIST=%a\n'
                 . 'ALBUM=%l\n'
                 . 'NUMBER=%n'
                 . 'DATE=%y'
                 . 'GENRE=%g'
                 ;

    #ID3v2
    my $mp3info = `mp3info2 -p"$format" '$file' 2>/dev/null`;
    if($mp3info !~ /^SUCCESS/){
      #ID3v1
      $mp3info = `mp3info -p"$format" '$file'`;
    }
    $title  = $1 if $mp3info =~ m/^\s*TITLE=(.*)$/mix;
    $artist = $1 if $mp3info =~ m/^\s*ARTIST=(.*)$/mix;
    $album  = $1 if $mp3info =~ m/^\s*ALBUM=(.*)$/mix;
    $number = $1 if $mp3info =~ m/^\s*NUMBER=(.*)$/mix;
    $date   = $1 if $mp3info =~ m/^\s*DATE=(.*)$/mix;
    $genre  = $1 if $mp3info =~ m/^\s*GENRE=(.*)$/mix;

  }
  if($file =~ /\.(mp3|ogg|flac)$/i){
    my $lltag = `lltag -S '$file'`;
    $title  = $1 if $lltag =~ m/^\s*TITLE=(.*)$/mix;
    $artist = $1 if $lltag =~ m/^\s*ARTIST=(.*)$/mix;
    $album  = $1 if $lltag =~ m/^\s*ALBUM=(.*)$/mix;
    $number = $1 if $lltag =~ m/^\s*NUMBER=(.*)$/mix;
    $date   = $1 if $lltag =~ m/^\s*DATE=(.*)$/mix;
    $genre  = $1 if $lltag =~ m/^\s*GENRE=(.*)$/mix;
  }elsif($file =~ /\.(wma)$/i){
    my %tags;
    if($useAudioWma){
      my $wma  = Audio::WMA->new($path);
      %tags = %{$wma->tags()};
    }else{
      print STDERR "WARNING: Cannot find perl module Audio::WMA\n";
    }

    for my $tag(keys %tags) {
      $title  = $tags{$tag} if $tag =~ m/^TITLE$/mix;
      $artist = $tags{$tag} if $tag =~ m/^ALBUMARTIST$/mix;
      $artist = $tags{$tag} if $tag =~ m/^AUTHOR$/mix;
      $album  = $tags{$tag} if $tag =~ m/^ALBUMTITLE$/mix;
      $number = $tags{$tag} if $tag =~ m/^TRACKNUMBER$/mix;
      $date   = $tags{$tag} if $tag =~ m/^YEAR$/mix;
      $genre  = $tags{$tag} if $tag =~ m/^GENRE$/mix;
    }
  }elsif($file =~ /\.(mp4|m4a|m4p|m4v|m4b)$/i){
    my $atomic = `AtomicParsley '$file' -t`;
    $title  = $1 if $atomic =~ m/^Atom\ "©NAM"\ contains:\ (.*)$/mix;
    $artist = $1 if $atomic =~ m/^Atom\ "©ART"\ contains:\ (.*)$/mix;
    $album  = $1 if $atomic =~ m/^Atom\ "©ALB"\ contains:\ (.*)$/mix;
    $number = $1 if $atomic =~ m/^Atom\ "trkn"\ contains:\ (.*)$/mix;
    $date   = $1 if $atomic =~ m/^Atom\ "©day"\ contains:\ (.*)$/mix;
    $genre  = $1 if $atomic =~ m/^Atom\ "(?:gnre|©gen)"\ contains:\ (.*)$/mix;
  }elsif($file =~ /\.wav$/i){
    #so wavs apparently have RIFF tags, but ive never seen em
  }else{
    print STDERR "Not a filetype we know how to read tags from: $path";
    return [];
  }

  return ($path, $title, $artist, $album, $number, $date, $genre);
}
