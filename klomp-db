#!/usr/bin/perl
use strict;
use warnings;

my @songSuffixes = ('mp3', 'flac', 'ogg', 'm4a', 'wma', 'wav');
                    #aiff

my $AUDIO_WMA = eval{ require Audio::WMA };
if(not $AUDIO_WMA){
  print STDERR "WARNING: Cannot find perl module Audio::WMA\n";
}
BEGIN{
  if(eval{ require Audio::WMA }){
    require Audio::WMA;
    Audio::WMA->import();
  }
}
  
my $TEXT_UNIDECODE = eval { require Text::Unidecode };
if(not $TEXT_UNIDECODE){
  print STDERR "WARNING: Cannot find perl module Text::Unidecode\n";
}
BEGIN{
  if(eval{ require Text::Unidecode }){
    require Text::Unidecode;
    Text::Unidecode->import;
  }
}

my $LINGUA_JA_ROMANIZE_JAPANESE =
  eval { require Lingua::JA::Romanize::Japanese };
if(not $LINGUA_JA_ROMANIZE_JAPANESE){
    print STDERR
      "WARNING: Cannot find perl module Lingua::JA::Romanize::Japanese\n";
}
BEGIN{
  if(eval { require Lingua::JA::Romanize::Japanese }){
    require Lingua::JA::Romanize::Japanese;
    Lingua::JA::Romanize::Japanese->import;
  }
}

my @infoCols = qw(library relpath filesize modified md5sum);
my @tagCols = qw(title artist album number date genre);
my @guessTagCols = map{$_ . "_guess"} @tagCols;
my @searchableCols = (@infoCols, @tagCols);
my @allCols = (@infoCols, @tagCols, @guessTagCols);

my $defaultDb = "$ENV{HOME}/.klompdb";

sub getAllLibs();
sub libpath($);
sub shell(@);
sub shellQuiet(@);
sub runSql($$;@);
sub runSqlBatch($\@;@);
sub cols($);
sub guess($$$$$$$);
sub transliterateJapanese($);
sub transliterateAscii($);
sub addSongs($$$);
sub wrapGuessCol($);
sub create($);
sub update($$);
sub missing($$);
sub add($$$);
sub search($$$$$);
sub info($$);
sub readTags($);

my $usage = "Usage: 
 $0 [db-path] -c|--create
   Make an empty sqlite db with necessary structures

 $0 [db-path] -u|--update [lib lib ...]
   Add or update the info in the db for music files in the given libs (or all)

 $0 [db-path] -m|--missing [lib lib ...]
   Check for missing files in given libs (or all), and prompt for removal.

 $0 [db-path] -a|--add lib song-rel-path
   Add or update the info in the db for the file at song-rel-path in lib

 $0 [db-path] -i|--info [song-rel-path or song-abs-path]
   Query for the info for a song, given the path of the song

 $0 [db-path] -s|--search query [OPTS]
   Query for relative paths to songs in given libs (or all), or more info.
   optional OPTS:
   --lib lib               optional libs to search (all if not given)
                           {can appear multiple times}
   -l|--limit LIMIT        optional integer limit on number of rows returned
   --col COL               optional col; returned as csv if any specified
                           {can appear multiple times}
                           by default, just relpath is returned.
                           allowed values: \n   @searchableCols

 [db-path] is optional and cannot start with a hyphen. {arg parsing => im lazy}
 if not given, the default is '$defaultDb'
";

sub main(@){
  die $usage if @_ == 0 or $_[0] =~ /^(-h|--help)/;

  my $db;
  if($_[0] !~ /^-/){
    $db = shift;
  }else{
    $db = $defaultDb;
  }

  my $cmd = shift() || '';

  if($cmd =~ /^(-c|--create)$/ and @_ == 0){
    if(-e $db){
      die "'$db' exists already\n";
    }
    create($db);
  }elsif($cmd =~ /^(-u|--update)$/){
    update($db, \@_);
  }elsif($cmd =~ /^(-m|--missing)$/){
    missing($db, \@_);
  }elsif($cmd =~ /^(-a|--add)$/ and @_ >= 2){
    my $lib = shift;
    my $songRelPath = shift;
    add($db, $lib, $songRelPath);
  }elsif($cmd =~ /^(-i|--info)$/ and @_ == 1){
    my $songRelPath = shift;
    info($db, $songRelPath);
  }elsif($cmd =~ /^(-s|--search)$/ and @_ >= 1){
    my $query = shift;

    my @libs;
    my $limit = 0;
    my @cols;
    my $mode = 'playlist';
    while(@_){
      my $arg = shift;
      if($arg =~ /^(-l|--limit)$/ && @_>0){
        $limit = shift;
        die $usage if $limit !~ /^\d+$/;
      }elsif($arg =~ /^(--col)$/ && @_>0){
        my $col = shift;
        my %isAllowed = map {$_ => 1} @searchableCols;
        die $usage if not defined $isAllowed{$col};
        push @cols, $col;
      }elsif($arg =~ /^(--lib)$/ && @_>0){
        my $lib = shift;
        push @libs, $lib;
      }else{
        die $usage;
      }
    }

    search($db, $query, $limit, \@cols, \@libs);
  }else{
    die $usage;
  }
}


sub create($){
  my $db = shift;
  if(-e $db){
    die "$db exists already\n";
  }
  runSql($db,
    "create table Songs ("
      . (join "", map {"$_ varchar,"} @allCols)
      . "PRIMARY KEY (library, relpath)"
    . ");");
}

sub update($$){
  my $db = shift;
  my @libs = @{shift()};
  for my $lib(@libs){
    my $libpath = libpath $lib;
    if(not -d $libpath){
      die "Invalid library path $libpath\n";
    }
    chdir $libpath;

    my $suffixPattern;
    for(my $i=0; $i<@songSuffixes; $i++){
      $suffixPattern .= ".*\\." . $songSuffixes[$i];
      if($i < $#songSuffixes){
        $suffixPattern .= "\\|";
      }
    }
    my @songRelPaths = shell 'find', '.', '-iregex', $suffixPattern;
    @songRelPaths = sort @songRelPaths;
    for (my $i=0; $i<@songRelPaths; $i++){
      my $songRelPath = $songRelPaths[$i];
      chomp $songRelPath;
      $songRelPath =~ s/^.\///;
      $songRelPaths[$i] = $songRelPath;
    }
    addSongs($db, $lib, \@songRelPaths);
  }
}

sub missing($$){
  my $db = shift;
  my @libs = @{shift()};
  for my $lib(@libs){
    chdir $lib;
    checkMissing($db, $lib);
  }
}

sub add($$$){
  my $db = shift;
  my $lib = shift;
  my $songRelPath = shift;
  
  my @songRelPaths;
  push @songRelPaths, $songRelPath;
  addSongs($db, $lib, \@songRelPaths);
}

sub parseQuotes($){
  my $query = shift;

  my $newQuery = '';
  my $endOfLastQuote = 0;

  my %quoteAtoms;
  my $atomId = 0;

  my $startQuote=0;
  my $endQuote;
 
  my $offset=0;

  my $len = length $query;
  while($offset >= 0 and $offset<$len){
    last if ($offset = index $query, '"', $offset) < 0;
    $startQuote = $offset;
    $offset++;
    while($offset >= 0 and $offset<$len){
      last if ($offset = index $query, '"', $offset) < 0;
      $endQuote = $offset;
      $offset++;
      my $nextChar = substr $query, $endQuote+1, 1;
      if($nextChar eq '"'){
        $offset++;
      }else{
        my $quoteAtom = substr $query, $startQuote+1, $endQuote-$startQuote-1;
        $quoteAtom =~ s/""/"/g;
        $quoteAtom =~ s/@/~at~/g;
        $quoteAtom =~ s/!/~bang~/g;
        $newQuery .= substr $query, $endOfLastQuote, $startQuote-$endOfLastQuote;
        $endOfLastQuote = $endQuote+1;
        $newQuery .= "~$atomId~";
        $quoteAtoms{$atomId++} = $quoteAtom;
        last;
      }
    }
  }
  $newQuery .= substr $query, $endOfLastQuote, $len-$endOfLastQuote;

  return ($newQuery, %quoteAtoms);
}

sub search($$$$$){
  my $db = shift;
  my $query = shift;
  my $limit = shift;
  my @cols = @{shift()};
  my @libs = @{shift()};
  
  my $libWhere = '';
  if(@libs > 0){
    $libWhere = "library in (" . (join ', ', map {"'$_'"} @libs) . ") and";
  }

  if(not @cols){
    @cols = ('relpath');
  }

  my %isGuessCol = map{$_ => 1} @guessTagCols;

  my @guessCols;
  my @namedCols;
  for my $col(@cols){
    my $guessCol;
    if(defined $isGuessCol{$col}){
      $guessCol = wrapGuessCol($col);
    }else{
      $guessCol = $col;
    }
    push @guessCols, $guessCol;
    push @namedCols, "$guessCol $col";
  }

  my $orderCols = join ",\n      ", @guessCols;
  my $orderBy = "ORDER BY\n      $orderCols";

  my $lim = $limit > 0 ? "LIMIT $limit" : '';

  $query =~ s/~/~~/g;
  
  my %quoteAtoms;
  ($query, %quoteAtoms) = parseQuotes $query;
  
  $query =~ s/\s*\&\&\s*/~and~/g;
  $query =~ s/\s*\|\|\s*/~or~/g;
  $query =~ s/\s*€\s*/~or~/g;  #euro symbol is synonym for ||
  $query =~ s/\s+/~and~/g;
  

  for my $atomId(keys %quoteAtoms){
    my $atom = $quoteAtoms{$atomId};
    $query =~ s/~$atomId~/$atom/;
  }

  my $selCols = join "\n       ,", @namedCols;
  my $sql = ''
    . "select $selCols \n"
    . "from Songs \n"
    . "where $libWhere (\n  (\n  ";

  $query .= '~term~';
  my @subqueries;
  while($query =~ /(.*?)(~and~|~or~|~term~)/g){
    push @subqueries, [$1, $2];
  }
  for my $sub(@subqueries){
    my $q = $$sub[0];

    my $neg = '';
    my $negAndOr = 'or';
    if($q =~ /^!/){
      $q =~ s/^!//;
      $neg = 'not';
      $negAndOr = 'and';
    }
    
    my $default = 1;
    my $title = 0;
    my $album = 0;
    my $artist = 0;
    my $genre = 0;
    my $path = 0;
    while($q =~ /^@[tlagp]/){
      $default = 0;
      $q =~ s/^@([tlagp])//;
      $title = 1 if $1 eq 't';
      $album = 1 if $1 eq 'l';
      $artist = 1 if $1 eq 'a';
      $genre = 1 if $1 eq 'g';
      $path = 1 if $1 eq 'p';
    }

    $q =~ s/~bang~/!/g;
    $q =~ s/~at~/@/g;
    
    $q =~ s/~~/~/g;
    $q =~ s/'/''/g;
    $q =~ s/%/\\%/g;
    my $term;
    $term = " and (" if $$sub[1] eq '~and~';
    $term = " or (" if $$sub[1] eq '~or~';
    $term = "" if $$sub[1] eq '~term~';
    my @wheres;
    if($default or $title){
      push @wheres, "title $neg like '\%$q\%'"
                    . " $negAndOr title_guess $neg like '\%$q\%'\n  ";
    }
    if($default or $album){
      push @wheres, "album $neg like '\%$q\%'"
                    . " $negAndOr album_guess $neg like '\%$q\%'\n  ";
    }
    if($default or $artist){
      push @wheres, "artist $neg like '\%$q\%'"
                    . " $negAndOr artist_guess $neg like '\%$q\%'\n  ";
    }
    if($genre){
      push @wheres, "genre $neg like '\%$q\%'\n  ";
    }
    if($path){
      push @wheres, "relpath $neg like '\%$q\%'\n  ";
    }
    $sql .= join(" $negAndOr ", @wheres) . ")$term\n  "
  }
  $sql .= "\n) \n$orderBy\n$lim;";
  
  #print STDERR $sql;
  
  if(@cols > 1){
    print runSql $db, $sql, '-csv';
  }else{
    print runSql $db, $sql;
  }
}

sub info($$){
  my $db = shift;
  my $songRelPath = shift;

  my $libWhere = '1=1';
  for my $lib(getAllLibs()){
    my $libpath = libpath $lib;
    if($songRelPath =~ s/^$libpath//){
      $libWhere = "library = '$lib'";
      last;
    }
  }
  $songRelPath =~ s/^\///;
  $songRelPath =~ s/'/''/g;

  my $sql = ''
    . "select * "
    . "from Songs "
    . "where relpath = '$songRelPath' "
    . "  and $libWhere;"
    ;
  my @rows = runSql($db, $sql, '-csv', '-header');
  die "file not found in db: $songRelPath\n" if @rows < 2;
  die "more than one song found, use abs path instead of '$songRelPath'\n"
    if @rows > 2;

  my @headers = cols $rows[0];
  my @data = cols $rows[1];

  if(@headers != @data){
    die "mismatched headers and data\n";
  }
  for(my $i=0; $i<@headers; $i++){
    print $headers[$i] . "=" . $data[$i] . "\n";
  }
}

sub wrapTokens(@){
  my @tokens;
  for my $token(@_){
    my $t = $token;
    $t =~ s/'/'\\''/g;
    push @tokens, "'$t'";
  }
  return @tokens;
}

sub getAllLibs(){
  my @libs = `klomp-lib`;
  chomp foreach @libs;
  return @libs;
}
sub libpath($){
  my $lib = shift;
  my $libpath = `klomp-lib -l $lib`;
  chomp $libpath;
  if(not $libpath){
    die "Error finding library path for $lib\n";
  }
  return $libpath;
}
sub shellQuiet(@){
  my $cmd = join ' ', wrapTokens(@_);
  return `$cmd 2>/dev/null`;
}
sub shell(@){
  my $cmd = join ' ', wrapTokens(@_);
  return `$cmd`;
}

sub runSql($$;@){
  my $db = shift;
  my $sql = shift;

  utf8::decode($sql);
  my @tokens;
  push @tokens, 'sqlite3';
  for my $opt(@_){
    push @tokens, $opt;
  }
  push @tokens, $db;
  push @tokens, $sql;

  return shell @tokens;
}
sub runSqlBatch($\@;@){
  my $db = shift;
  my @sqls = @{shift()};
  my $sql = join ";\n", @sqls;
  $sql = "BEGIN;\n$sql;\nEND;\n";
  runSql $db, $sql, @_;
}

sub cols($){
  my $csvrow = shift;
  chomp $csvrow;
  my @cols;
  while($csvrow =~ /
    (?:
      "()"
      |
      "(.*?(?<!"))"
      |
      ([^,]+)
    )
    (?:,|$)
  /gsxi){
    my $col;
    $col = $1 if defined $1;
    $col = $2 if defined $2;
    $col = $3 if defined $3;
    $col =~ s/""/"/g;
    push @cols, $col;
  }
  return @cols;
}

sub guess($$$$$$$){
  my ($songRelPath, $title, $artist, $album, $number, $date, $genre) = @_;
  my ($title_guess, $artist_guess, $album_guess,
    $number_guess, $date_guess, $genre_guess);

  $title_guess = transliterateJapanese $title;
  $artist_guess = transliterateJapanese $artist;
  $album_guess = transliterateJapanese $album;
  $number_guess = transliterateJapanese $number;
  $date_guess = transliterateJapanese $date;
  $genre_guess = transliterateJapanese $genre;

  $title_guess = transliterateAscii $title if not defined $title_guess;
  $artist_guess = transliterateAscii $artist if not defined $artist_guess;
  $album_guess = transliterateAscii $album if not defined $album_guess;
  $number_guess = transliterateAscii $number if not defined $number_guess;
  $date_guess = transliterateAscii $date if not defined $date_guess;
  $genre_guess = transliterateAscii $genre if not defined $genre_guess;

  my ($filename, $innerDir, $outerDir);
  if($songRelPath =~ /([^\/]*) \/ ([^\/]*) \/ ([^\/]*)$/x){
    $filename = $3;
    $innerDir = $2;
    $outerDir = $1;
  }elsif($songRelPath =~ /([^\/]*) \/ ([^\/]*)$/x){
    $filename = $2;
    $innerDir = '';
    $outerDir = $1;
  }elsif($songRelPath =~ /([^\/]*)$/x){
    $filename = $1;
    $innerDir = '';
    $outerDir = '';
  }

  if(not defined $title or length $title == 0){
    $title_guess = $filename;
    my $suffixRegex;
    for (my $i=0; $i<@songSuffixes; $i++){
      $suffixRegex .= '.' . $songSuffixes[$i];
      if($i < $#songSuffixes){
        $suffixRegex .= '|';
      }
    }
    $title_guess =~ s/($suffixRegex)$//i;
    $title_guess =~ s/^[0-9 \-_]+//;
    #$title_guess =~ s/_/ /g;
  }
  if(not defined $artist or length $artist == 0){
    $artist_guess = $outerDir;
    #$artist_guess =~ s/_/ /g;
  }
  if(not defined $album or length $album == 0){
    $album_guess = $innerDir;
    #$album_guess =~ s/_/ /g;
  }
  if(not defined $number or length $number == 0){
    if($filename =~ /^\s*(\d+)\s*/){
      $number_guess = $1;
    }
  }
  if(not defined $date or length $date == 0){
    if($songRelPath =~ /(19\d\d|20\d\d)/){
      $date_guess = $1;
    }
  }

  $title_guess = '' if not defined $title_guess;
  $artist_guess = '' if not defined $artist_guess;
  $album_guess = '' if not defined $album_guess;
  $number_guess = '' if not defined $number_guess;
  $date_guess = '' if not defined $date_guess;
  $genre_guess = '' if not defined $genre_guess;

  return ($title_guess, $artist_guess, $album_guess, $number_guess,
    $date_guess, $genre_guess);
}

sub transliterateJapanese($){
  return undef if not $LINGUA_JA_ROMANIZE_JAPANESE;
  my $arg = shift;
  if(not defined $arg or length $arg == 0){
    return $arg;
  }
  utf8::decode($arg);
  my @parts;
  my $theRest;
  while($arg =~/
      (.*?)
      ((?:\p{Hiragana}|\p{Katakana}|\p{Han})+)
      (?=(.*$))
    /gsxi){
    my $nonjap = $1;
    my $jap = $2;
    $theRest = $3;
    my $conv = Lingua::JA::Romanize::Japanese->new();
    my $romaji = $conv->chars($jap);

    push @parts, $nonjap;
    if(not $? and defined $romaji){
      $romaji =~ s/\s+//g;
      push @parts, $romaji;
    }else{
      push @parts, $jap;
    }
  }
  utf8::encode($arg);
  if(@parts > 0){
    push @parts, $theRest;
    my $res = join '', @parts;
    utf8::encode($res);
    return $res;
  }
  return undef;
}

sub transliterateAscii($){
  return undef if not $TEXT_UNIDECODE;
  my $arg = shift;
  return undef if not defined $arg or $arg eq '';
  
  my $oldArg = $arg;

  utf8::decode($arg);
  $arg = unidecode $arg;
  utf8::encode($arg);
  
  if($oldArg eq $arg){
    return undef;
  }else{
    return $arg;
  }
}

sub checkMissing($$){
  my $db = shift;
  my $lib = shift;
  my $libpath = libpath $lib;

  my @songRelPaths =
    runSql $db, "select relpath from songs where library = '$lib'";
  my $len = @songRelPaths;
  my $piece = 1+$len / 10;
  print "checking $len songs from the db for missing\n";
  my $count = 0;
  my @missing;
  my @deleteSql;
  for my $songRelPath(@songRelPaths){
    chomp $songRelPath;
    if(not -e "$libpath/$songRelPath"){
      print "$songRelPath\n";
      push @missing, $songRelPath;
      $songRelPath =~ s/'/''/g;
      push @deleteSql, ''
          . "delete from Songs "
          . "where relpath = '$songRelPath' "
          . "  and library = '$lib';\n"
          ;
    }
  }
  my $missingCount = scalar @missing;
  print "  finished, $missingCount missing\n";
  if($missingCount > 0){
    print "Remove above missing files from lib? (y/N) ";
    if(<STDIN> =~ /^y$/i){
      my $limit = 30;
      my $lastEnd = -1;
      for(my $i=$limit; $i<@deleteSql; $i+=$limit){
        my ($start, $end) = ($lastEnd+1, $i);
        print "Removing songs $start-$end\n";
        my @toRun = @deleteSql[$start .. $end];
        runSqlBatch $db, @toRun;
        $lastEnd = $i;
      }
      if($lastEnd < $#deleteSql){
        my ($start, $end) = ($lastEnd+1, $#deleteSql);
        print "Removing songs $start-$end\n";
        my @toRun = @deleteSql[$start .. $end];
        runSqlBatch $db, @toRun;
      }
    }
  }
}

sub addSongs($$$){
  my $db = shift;
  my $lib = shift;
  my @songRelPaths = @{scalar shift};

  my $libpath = libpath $lib;
  
  my %dbInfo;
  for my $line(runSql($db, 'select filesize, modified, relpath from Songs')){
    $line =~ /(\d+)\|(\d+)\|(.*)\n/;
    my @fs_mod = ($1, $2);
    $dbInfo{$3} = \@fs_mod;
  }

  my $songCount = @songRelPaths;
  my $handledCount = 0;
  my $skippedCount = 0;
  print "Adding/updating $songCount songs from the filesystem\n";
  
  my $count = 0;
  my @stmts = ();
  my $limit = 30;
  for my $songRelPath(@songRelPaths){
    my ($dbSize, $dbMod);
    
    my $info = $dbInfo{$songRelPath};
    my $dbExists = defined $info;
    if($dbExists){
      my @infoArr = @{$info};
      $dbSize = $infoArr[0];
      $dbMod = $infoArr[1];
    }
  
    my $stat = shellQuiet 'stat', '--format', '%s:%Y', "$libpath/$songRelPath";
    if($? or $stat !~ /(\d+):(\d+)/){
      print STDERR "file not found: $libpath/$songRelPath\n";
      next;
    }
    my $filesize = $1;
    my $modified = $2;

    if($dbExists and $dbSize == $filesize and $dbMod == $modified){
      $skippedCount++;
      next;
    }
    
    my $md5sum = '';

    my ($path, $title, $artist, $album, $number, $date, $genre) =
      readTags("$libpath/$songRelPath");

    my ($title_guess, $artist_guess, $album_guess,
        $number_guess, $date_guess, $genre_guess) = guess(
          $songRelPath, $title, $artist, $album, $number, $date, $genre);

    my %cols;
    $cols{title} = $title;
    $cols{artist} = $artist;
    $cols{album} = $album;
    $cols{number} = $number;
    $cols{date} = $date;
    $cols{genre} = $genre;

    $cols{title_guess} = $title_guess;
    $cols{artist_guess} = $artist_guess;
    $cols{album_guess} = $album_guess;
    $cols{number_guess} = $number_guess;
    $cols{date_guess} = $date_guess;
    $cols{genre_guess} = $genre_guess;
    
    $cols{library} = $lib;
    $cols{relpath} = $songRelPath;
    $cols{filesize} = $filesize;
    $cols{modified} = $modified;
    $cols{md5sum} = $md5sum;

    my @allVals;
    for my $col(@allCols){
      my $val = $cols{$col};
      chomp $val;
      $val =~ s/'/''/g;
      $val = "'$val'";
      push @allVals, $val;
    }

    print "$songRelPath\n";
    push @stmts, "delete from Songs where relpath='$cols{relpath}'";
    push @stmts, ''
      . " insert into Songs"
      . " (" . join(", ", @allCols) . ")"
      . " values"
      . " (" . join(", ", @allVals) . ")"
      ;

    if(++$count >= $limit){
      print "  adding $count.." .
        "{total: $songCount handled: $handledCount skipped: $skippedCount}\n";
      runSqlBatch $db, @stmts;
      $handledCount += $count;
      @stmts = ();
      $count = 0;
    }
  }
  if($count > 0){
    print "  adding $count.." .
      "{total: $songCount handled: $handledCount skipped: $skippedCount}\n";
    runSqlBatch $db, @stmts;
    $handledCount += $count;
    @stmts = ();
    $count = 0;
  }
  print "  handled $handledCount new or modified songs\n";
  print "  skipped $skippedCount unmodified songs {same size and mod date}\n";
}

sub wrapGuessCol($){
  my $col = shift;
  my $colGuess = $col . '_guess';
  return
    "IFNULL(".
      "NULLIF(".
        "$col,".
        "''".
      "),".
      "$colGuess)";
}

sub readTags($){
  my $file = shift;
  my $path = $file;
  $file =~ s/'/'\\''/g;

  my $title='';
  my $artist='';
  my $album='';
  my $number='';
  my $date='';
  my $genre='';
  if($file =~ /\.mp3$/i){
    my $eyeD3 = `eyeD3 --no-color '$file'`;
    $eyeD3 =~ s/\t\t/\n/g;

    $title  = $1 if $eyeD3 =~ /^title: (.*)/mi;
    $artist = $1 if $eyeD3 =~ /artist: (.*)\n/mi;
    $album  = $1 if $eyeD3 =~ /^album: (.*)$/mi;
    $number = $1 if $eyeD3 =~ /^track: (.*)$/mi;
    $date   = $1 if $eyeD3 =~ /^year: (.*)$/mi;
    $genre  = $1 if $eyeD3 =~ /^genre: (.*)$/mi;

    $genre =~ s/ \(id \d+\)$//; #trim the genre id
  }elsif($file =~ /\.(ogg|flac)$/i){
    my $lltag = `lltag -S '$file'`;
    $title  = $1 if $lltag =~ m/^\s*TITLE=(.*)$/mix;
    $artist = $1 if $lltag =~ m/^\s*ARTIST=(.*)$/mix;
    $album  = $1 if $lltag =~ m/^\s*ALBUM=(.*)$/mix;
    $number = $1 if $lltag =~ m/^\s*NUMBER=(.*)$/mix;
    $date   = $1 if $lltag =~ m/^\s*DATE=(.*)$/mix;
    $genre  = $1 if $lltag =~ m/^\s*GENRE=(.*)$/mix;
  }elsif($file =~ /\.(wma)$/i){
    my %tags;
    if($AUDIO_WMA){
      my $wma  = Audio::WMA->new($path);
      %tags = %{$wma->tags()};
    }else{
      print STDERR "WARNING: no tags for $file, missing Audio::WMA\n";
    }

    for my $tag(keys %tags) {
      $title  = $tags{$tag} if $tag =~ m/^TITLE$/mix;
      $artist = $tags{$tag} if $tag =~ m/^ALBUMARTIST$/mix;
      $artist = $tags{$tag} if $tag =~ m/^AUTHOR$/mix;
      $album  = $tags{$tag} if $tag =~ m/^ALBUMTITLE$/mix;
      $number = $tags{$tag} if $tag =~ m/^TRACKNUMBER$/mix;
      $date   = $tags{$tag} if $tag =~ m/^YEAR$/mix;
      $genre  = $tags{$tag} if $tag =~ m/^GENRE$/mix;
    }
  }elsif($file =~ /\.(mp4|m4a|m4p|m4v|m4b)$/i){
    my $atomic = `AtomicParsley '$file' -t`;
    $title  = $1 if $atomic =~ m/^Atom\ "©NAM"\ contains:\ (.*)$/mix;
    $artist = $1 if $atomic =~ m/^Atom\ "©ART"\ contains:\ (.*)$/mix;
    $album  = $1 if $atomic =~ m/^Atom\ "©ALB"\ contains:\ (.*)$/mix;
    $number = $1 if $atomic =~ m/^Atom\ "trkn"\ contains:\ (.*)$/mix;
    $date   = $1 if $atomic =~ m/^Atom\ "©day"\ contains:\ (.*)$/mix;
    $genre  = $1 if $atomic =~ m/^Atom\ "(?:gnre|©gen)"\ contains:\ (.*)$/mix;
  }elsif($file =~ /\.wav$/i){
    #so wavs apparently have RIFF tags, but ive never seen em
  }else{
    print STDERR "Not a filetype we know how to read tags from: $path";
    return [];
  }

  return ($path, $title, $artist, $album, $number, $date, $genre);
}

&main(@ARGV);
