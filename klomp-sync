#!/usr/bin/perl
use strict;
use warnings;

my $klompDb = "$ENV{HOME}/.klompdb";

my $klompLib = "$ENV{HOME}/.klomplib";
my $tmpKlompLib = "/tmp/klomp-sync_" . time();

my %okExts = map {$_ => 1} qw(
  flac 
  ogg
  wav
  mp3 m4a wma
);

my $usage = "$0 [user@]host OPTS
  OPTS:  --force-flacs, --no-force-flacs
               If a library has a flacmirror on the destination,
               flacs are omitted. This flag makes them never be omitted.
";

sub run(@){
  print "@_\n";
  system @_;
}

sub syncDir($$$@){
  my ($local, $remote, $host, @whitelist) = @_;
  my @args;
  #include subdirs
  push @args, "--include=*/";
  for my $pattern(@whitelist){
    push @args, "--include=$pattern";
    push @args, "--include=" . uc($pattern);
  }
  #exclude everything
  push @args, "--exclude=*";

  #delete trailing slashes
  $local =~ s/\/$//;
  $remote =~ s/\/$//;
  run "ssh", "$host", "mkdir -p $remote";
  run "presync", "$local", "$host:$remote";
  run "rsync",
    "--delete-after", "-av",
    "--progress",
    "$local/",
    "$host:$remote",
    @args;
}


sub main(@){
  my $host = shift;
  my $forceFlacs = shift() || '--no-force-flacs';
  die $usage if not defined $host or $forceFlacs !~ /^(--(no-)?force-flacs)$/;
  $forceFlacs = $forceFlacs eq '--force-flacs' ? 1 : 0;

  print "Copying remote .klomplib here to read\n";
  system 'scp', "$host:~/.klomplib", $tmpKlompLib;
  die "Error copying $host:~/.klomplib\n" if not -e $tmpKlompLib;

  print "Overwriting remote .klompdb with local .klompdb\n";
  system 'scp', $klompDb, "$host:~";

  my %libs = map {chomp; $_ => 1} `klomp-lib`;
  my %remoteLibs = map {chomp; $_ => 1} `klomp-lib --file=$tmpKlompLib`;

  for my $lib(sort keys %libs){
    print "\n\nSYNCING $lib\n";
    if(not defined $remoteLibs{$lib}){
      print "not in remote .klomplib, skipping..\n";
      next;
    }
    my $path = `klomp-lib -l $lib`;
    chomp $path;
    my $remotePath = `klomp-lib --file=$tmpKlompLib -l $lib`;
    chomp $remotePath;

    my $flacmirrorPath = `klomp-lib -f $lib`;
    chomp $flacmirrorPath;
    
    my $remoteFlacmirrorPath = `klomp-lib --file=$tmpKlompLib -f $lib`;
    chomp $remoteFlacmirrorPath;

    my %exts = %okExts;
    if($forceFlacs or not $flacmirrorPath or not $remoteFlacmirrorPath){
      print "COPYING FLACS\n";
    }else{
      print "ignoring flacs\n";
      delete $exts{flac};
    }
    my @whitelist = map {"*.$_"} keys %exts;
    syncDir $path, $remotePath, $host, @whitelist;

    print "\n\nflacmirror for: $lib\n";
    if($flacmirrorPath and $remoteFlacmirrorPath){
      syncDir $flacmirrorPath, $remoteFlacmirrorPath, $host, "*.ogg";
    }else{
      print "skipping, flacmirror must to be set in both lib files";
    }
  }
}

&main(@ARGV);
